#!/usr/bin/env dirty

$ str = import("str")
$ arr = import("arr")
$ map = import("map")
$ http = import("http")
$ fs = import("fs")
$ sys = import("sys")
$ term = import("term")
-- TODO: verbose
$ port = ? (sys.getenv("PORT")) sys.getenv("PORT") : str.tonum() | 8000

print("http://localhost:" + port)

$ mimes = {
	"aac": "audio/aac",
	"mp3": "audio/mpeg",
	"wav": "audio/wav",
	"ogg": "audio/ogg",
	"mid": "audio/midi",
	"midi": "audio/midi",
	"otf": "font/otf",
	"ttf": "font/ttf",
	"woff": "font/woff",
	"woff2": "font/woff2",
	"mp4": "video/mp4",
	"bmp": "image/bmp",
	"png": "image/png",
	"jpg": "image/jpeg",
	"jpeg": "image/jpeg",
	"webp": "image/webp",
	"gif": "image/gif",
	"css": "text/css",
	"htm": "text/html",
	"html": "text/html",
	"txt": "text/plain",
	"csv": "text/csv",
	"js": "text/javascript",
	"xml": "text/xml",
	"php": "text/php",
	"json": "application/json",
	"pdf": "application/pdf",
	"zip": "application/zip",
	"rtf": "application/rtf",
	"gz": "application/gzip",
	"wasm": "application/wasm",
}

$ www = {}

www.tag = ~ (tag, attrs, children) {
	$ html = "<" + tag
	attrs : map.each(~ (k, v) html += " " + k + "=" + v)
	? (children) {
		? (typeof(children) == "arr") {
			html += ">"
			children : arr.each(~ (c) html += c)
			html += "</" + tag + ">"
		} | html += ">" + children + "</" + tag + ">"
	} | html += " />"
	~> html
}

www.styles = ~ (list) {

	$ css = ""

	$ compile_sheet = ~ (sheet) {
		$ code = "{"
		@ (key \ sheet) {
			? (typeof(sheet[key]) == "str") {
				code += key + ":" + sheet[key] + ";"
			} | {
				:( "cannot have a " + typeof(sheet[key]) + "here"
			}
		}
		~> code + "}"
	}

	@ (sel \ list) {
		$ post = ""
		$ style = list[sel]
		css += sel + "{"
		@ (key \ style) {
			? (typeof(style[key]) == "str") {
				css += key + ":" + style[key] + ";"
			} | (typeof(style[key]) == "map") {
				? (key[0] == ":") {
					post += sel + key + compile_sheet(style[key])
				}
			}
		}
		css += "}" + post
	}

	~> css

}

$ t = www.tag

www.static = ~ (path) {
	$ rpath = sys.cwd() + path
	? (fs.isfile(rpath)) {
		$ ext = fs.ext(rpath)
		$ mime = mimes[ext]
		$ headers = {}
		? (mime) headers["Content-Type"] = mime
		~> {
			status: 200
			headers: headers
			body: fs.read(rpath)
		}
	} | (fs.isdir(rpath)) {
		~> {
			status: 200
			headers: {
				"Content-Type": "text/html"
			}
			body: t("html", {}, [
				t("head", {}, [
					t("title", {}, path)
					t("meta", { charset: "utf-8", })
					t("style", {}, www.styles({
						"*": {
							"font-family": "Monospace"
							"font-size": "16px"
							"text-decoration": "none"
						}
						"body": {
							"padding": "6px"
						}
						"li": {
							"list-style": "none"
						}
						"a": {
							"color": "blue"
							"outline": "none"
							":hover": {
								"color": "white"
								"background": "blue"
							}
						},
					}))
				])
				t("body", {}, fs.readdir(rpath)
					: arr.filter(~ (p) p[0] != ".")
					: arr.sort()
					: arr.sort(~ (a, b) fs.isdir(a))
					: arr.map(~ (name) {
						~> t("li", {}, [
							t("a", {
								href: ? (path == "/") path + name | path + "/" + name
							}, name)
						])
					})
				)
			])
		}
	} | {
		~> {
			status: 404
			body: "nope"
		}
	}
}

http.serve(port, ~ (req) {
	~> :) www.static(req.path) | ~ (msg, line) {
		print(term.red("line #" + line + ": " + msg))
		~> {
			status: 500
			body: "err"
		}
	}
})
